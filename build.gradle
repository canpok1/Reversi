apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'application'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

// 文字コード設定
def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava, javadoc]*.options*.encoding = defaultEncoding

dependencies {
    compile fileTree(dir:"Reversi/lib", include: "*.jar")
    testCompile fileTree(dir:"Reversi/lib", include: "*.jar")
    testCompile 'org.jmockit:jmockit:1.12'
    testCompile 'org.jmockit:jmockit-coverage:1.12'
    testCompile 'junit:junit:4.10'
}

// ディレクトリ構成
sourceSets {
    main {
        java {
            srcDir 'Reversi/src'
        }
    }
    test {
        java {
            srcDir 'ReversiTest/src'
        }
    }
}

// Gradleラッパーのインストールタスク
task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

test {
    // テストが二重実行される問題の対策
    exclude '**/*$*'
}


findbugsMain {
    effort = "max"
    reportLevel = "high"
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

findbugsTest {
    effort = "max"
    reportLevel = "high"
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

[checkstyleMain, checkstyleTest]*.ignoreFailures = true

// Mainクラス設定
mainClassName = "main.GuiMain"

defaultTasks 'clean', 'javadoc', 'build', 'distZip'
